apply plugin: 'com.android.library'

ext.pluginVersionCode = 1
ext.pluginVersionName = "3.3.2"

android {
    compileSdk 34
    //buildToolsVersion "34.0.0"
    namespace "com.onecat.godotappodeal"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode pluginVersionCode
        versionName pluginVersionName
        multiDexEnabled true
        useLibrary 'org.apache.http.legacy'
    }
    ndkVersion '23.2.8568313'
    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "GodotAppodeal.$pluginVersionName.${variant.name}.aar"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.appodeal.ads:sdk:3.3.2.0'
    implementation 'com.bigossp:max-mediation:4.8.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    compileOnly fileTree(dir: 'libs', includes: ['godot-lib*.aar'])
}

tasks.register('buildIntoRelease', Copy) {
    group = "build"
    dependsOn tasks.build
    from([buildDir.absolutePath, 'outputs', 'aar'].join(File.separator))
    include("GodotAppodeal.${pluginVersionName}.release.aar")
    into([rootDir.absolutePath, 'release'].join(File.separator))
}