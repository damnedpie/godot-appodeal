apply plugin: 'com.android.library'

ext.pluginVersionCode = 1
ext.pluginVersionName = "3.7.0"
ext.zipName = "godot-appodeal"
ext.godotVersion = "3.6"

android {
    compileSdk 34
    namespace "com.onecat.godotappodeal"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34
        versionCode pluginVersionCode
        versionName pluginVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    ndkVersion '23.2.8568313'
    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "GodotAppodeal.$pluginVersionName.${variant.name}.aar"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.appodeal.ads:sdk:3.7.0.0'
    implementation 'org.godotengine:godot:3.6.0.stable'
}

tasks.register('buildIntoRelease', Copy) {
    group = "build"
    dependsOn tasks.build
    from([buildDir.absolutePath, 'outputs', 'aar'].join(File.separator))
    include("GodotAppodeal.${pluginVersionName}.release.aar")
    into([rootDir.absolutePath, 'release'].join(File.separator))
}

tasks.register('buildIntoReleaseAndPrepareForGithub', Zip) {
    group = "build"
    dependsOn tasks.buildIntoRelease
    from([rootDir.absolutePath, 'release'].join(File.separator))
    include("*.gd", "*.gdap", "*.aar")
    archiveFileName = "${zipName}-${pluginVersionName}-${godotVersion}.zip"
    destinationDirectory = file([rootDir.absolutePath, 'release'].join(File.separator))
    def metaTxt = new File([rootDir.absolutePath, 'release', "github-meta.txt"].join(File.separator))
    metaTxt.text = "SDK ${pluginVersionName}, Godot ${godotVersion}+, rev.${pluginVersionCode}"
    metaTxt.text += "\nSDK${pluginVersionName}GD${godotVersion}rev${pluginVersionCode}"
}
